import groovyx.net.http.ContentType
import org._10ne.gradle.rest.RestTask

/*
 * Copyright 2014 Andrew Reitz
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: rootProject.file('gradle/credentials.gradle')

subprojects {
  buildscript {
    repositories {
      jcenter()
    }
    dependencies {
      classpath 'com.android.tools.build:gradle:1.0.0-rc1'
      classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6'
      classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.0.1'
      classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:1.12.3'
    }
  }

  repositories {
    jcenter()
  }

  group = GROUP
  version = "$VERSION_NAME${project.hasProperty('release') ? '' : '-SNAPSHOT'}"
}

ext {
  minSdkVersion = 7
  compileSdkVersion = 21
  buildToolsVersion = '21.1.0'
}

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "org._10ne.gradle:rest-gradle-plugin:0.3.1"
  }
}

apply plugin: "org.10ne.rest"

task bintraySign(type: RestTask) {
  description = "Sign the release build on bintray"
  httpMethod = 'post'
  uri = "https://api.bintray.com/gpg/pieces/maven/velcro-parts/versions/${VERSION_NAME}"
  username = bintrayUsername
  password = bintrayKey
  requestBody = [passphrase: signingPassword]
  contentType = ContentType.JSON
}
